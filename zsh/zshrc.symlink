# shortcut to this dotfiles path is $DOTFILES
export DOTFILES_ROOT=$HOME/.dotfiles

# all of our zsh files
typeset -U config_files
config_files=($DOTFILES_ROOT/**/*.zsh)

# load the path files
for file in ${(M)config_files:#*/path.zsh}
do
  echo "loading $file"
  source $file
done

# load everything but the path and completion files
for file in ${${config_files:#*/path.zsh}:#*/completion.zsh}
do
  echo "loading $file"
  source $file
done

# initialize autocomplete here, otherwise functions won't be loaded
autoload -U compinit
compinit -i

# load every completion after autocomplete loads
for file in ${(M)config_files:#*/completion.zsh}
do
  echo "loading $file"
  source $file
done

# load completer installed by brew

unset config_files
